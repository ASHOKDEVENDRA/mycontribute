%MACRO VARYSIGMAS(NAME=,SIGMA=);

%IF &SIGMA. <= 0 %THEN %LET SIGMA = 0.00000001;
 %LET SIGMAINV = %SYSEVALF(1 / &SIGMA.);
%PUT SIGMA=&SIGMA;
%PUT SIGMAINV=&SIGMAINV;

DATA VARYSIGMAS&NAME;
	LENGTH NAME $10.;
	/*UNEQUAL STEPS DO NOT ALLOW USE OF BY*/
	/*DO SIGMAINV = 1/(&SIGMA. - 0.1*&SIGMA.) TO 1/(&SIGMA. + 0.1*&SIGMA.) BY (1/(0.1*&SIGMA.))/2;*/
	DO SIGMAINV = 1/(1.1*&SIGMA.), 1/(1.05*&SIGMA.), 1/(&SIGMA.), 1/(0.95*&SIGMA.), 1/(0.9*&SIGMA.);
		SIGMA=1/SIGMAINV;
		NAME="&NAME";
		OUTPUT;
	END;
RUN;

%MEND;

%VARYSIGMAS(NAME=0,SIGMA=0);
%VARYSIGMAS(NAME=POINT01,SIGMA=0.01);
%VARYSIGMAS(NAME=POINT1,SIGMA=0.1);
%VARYSIGMAS(NAME=1,SIGMA=1);
%VARYSIGMAS(NAME=1000,SIGMA=1000);

DATA VARYSIGMA;
	SET VARYSIGMAS:;
RUN;

PROC SQL;
TITLE "SIGMAINV Bounds Based on Varying SIGMA";
TITLE2 "Middle Value Slightly Closer to Lower Bound (Below Midpoint of Bounds)";
	SELECT A.*, B.BOUNDSMIDPOINT FORMAT = 20.5
		FROM VARYSIGMA A
	LEFT JOIN 
	(SELECT NAME, (MAX(SIGMAINV) + MIN(SIGMAINV))/2 AS BOUNDSMIDPOINT
		FROM VARYSIGMA
		GROUP BY NAME) B
	ON A.NAME = B.NAME
	ORDER BY SIGMAINV;
QUIT;
TITLE; TITLE2;


%MACRO VARYSIGMAINVS(NAME=,SIGMA=);

%IF &SIGMA. <= 0 %THEN %LET SIGMA = 0.00000001;
 %LET SIGMAINV = %SYSEVALF(1 / &SIGMA.);

DATA VARYSIGMAINVS&NAME;
	LENGTH NAME $10.;
	/*SIGMAINV(+/-)10%*/
	/*DO SIGMAINV = (&SIGMAINV. - 0.1*1/(&SIGMA.)) TO (&SIGMAINV. + 0.1*1/(&SIGMA.)) BY (0.1*1/(&SIGMA.))/2;*/
	/*WHICH IS THE SAME AS*/
	DO SIGMAINV = 0.9*&SIGMAINV. TO 1.1*&SIGMAINV. BY (0.1*&SIGMAINV.)/2;
		SIGMA=1/SIGMAINV;
		NAME="&NAME";
		OUTPUT;
	END;
RUN;

%MEND;

%VARYSIGMAINVS(NAME=0,SIGMA=0);
%VARYSIGMAINVS(NAME=POINT01,SIGMA=0.01);
%VARYSIGMAINVS(NAME=POINT1,SIGMA=0.1);
%VARYSIGMAINVS(NAME=1,SIGMA=1);
%VARYSIGMAINVS(NAME=1000,SIGMA=1000);

DATA VARYSIGMAINV;
	SET VARYSIGMAINVS:;
RUN;

PROC SQL;
TITLE "SIGMAINV Bounds Based on Directly Varying SIGMAINV";
TITLE2 "Middle Value Equal to Midpoint of Bounds";
	SELECT A.*, B.BOUNDSMIDPOINT FORMAT = 20.5
		FROM VARYSIGMAINV A
	LEFT JOIN 
	(SELECT NAME, (MAX(SIGMAINV) + MIN(SIGMAINV))/2 AS BOUNDSMIDPOINT
		FROM VARYSIGMAINV
		GROUP BY NAME) B
	ON A.NAME = B.NAME
	ORDER BY SIGMAINV;
QUIT;
TITLE; TITLE2;
